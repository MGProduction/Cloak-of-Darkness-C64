// -------------
// meta commands
// -------------

verb:$unknown
	msg:Sorry, I don't understand

objclass:scenary
	attr:visible
	
objclass:scenaryobj
	attr:visible
	
objclass:actor
	attr:actor+visible
	
verbclass:genericverb
	withobj:$none
		msg:#V what?
	withobj:$unknown
		msg:I don't see that
	withobj:$every		
		ifobjin:$this,$inventory
			msg:What for?
		else
			ifobjin:$this,$here
				msg:What for?
			else
				msg:I don't see it here		

// -------------
// meta commands
// -------------
					
verb:onfirst
verb:onenter
	clear
	msg:$roomdesc
	
//verb:onenter
//	msg:You are now in the +
//	msg:$roomname

verb:help|h
	msg:In this kind of games you can do actions simply writing short 
		commands in \yVERB+OBJECT \wform.
		With them you can \yEXAMINE \welements, \yTAKE \wor \yDROP \witems and so on.
		There are also special commands like \yINVENTORY \wthat will show what
		you're carrying with you.
	
verb:inventory|i	
	ifobjinattr:$any,$inventory,takeable
		msg:You are carrying: +
		listobjin:$inventory,takeable
	else
		msg:You aren't carrying anything.
		
verb:score
	msg:Your current score is +
	msg:$score

verb:quit|q
	msg:Are you sure? (Y/N)
	getkey
	ifkey:Y
		quit
		
verb:restart
	msg:Are you sure? (Y/N)
	getkey
	ifkey:Y
		start
		
verb:save
	save
	msg:Done
	
verb:load
	msg:Are you sure? (Y/N)
	getkey
	ifkey:Y
		load
		
// -------------
// direction
// -------------

verb:up|go_up
verb:down|go_down
verb:s|south|go_south
verb:n|north|go_north
verb:w|west|go_west
verb:e|east|go_east
verb:se|southest|south-east|go_southeast
verb:sw|southwest|south-west|go_southwest
verb:ne|northeast|north-east|go_northeast
verb:nw|nortwest|north-west|go_northwest
	msg:You can't go that way
		
		
// -------------
// base actions
// -------------
	
genericverb:x|examine|look|l|look_at
	withobj:$none
		msg:$roomdesc
		ifobjinattr:$any,$here,visible
			msg:You notice: +
			listobjin:$here,visible
	withobj:$every
		ifobjin:$this,$inventory
			msg:$objdesc
		else
			ifobjinattr:$this,$here,visible
				msg:$objdesc
			else
				msg:I don't see that here

genericverb:take|get|pick
	withobj:$every
		ifobjin:$this,$here
			ifobj:$this,takeable+visible
				putobj:$this,$inventory
				msg:Taken
			else
				msg:I can't take it
		else
			ifobjin:$this,$inventory
				msg:You already have it
			else
				msg:I don't see it here
				
genericverb:drop|put_down
	withobj:$every
		ifobjin:$this,$inventory
			ifobj:$this,takeable+visible
				putobj:$this,$here
				msg:Dropped
			else
				msg:I can't drop it
		else
			msg:You don't have it

genericverb:use

genericverb:push

genericverb:pull

genericverb:open

genericverb:close

genericverb:lock

genericverb:unlock



