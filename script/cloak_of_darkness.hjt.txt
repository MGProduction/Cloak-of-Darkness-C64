config
	name:Cloak of Darkness [C64]
	version:1.2
	author:Marco Giorgini
	authorcontact:@marcogiorgini
	engine:StoryTllrC64
	licence:MIT
	info:Cloak of Darkness - http://www.firthworks.com/roger/cloak/
	font:png\font.png
	splity:96
	binary:no
	imgfolder:img\
include:stdlib.hjt.txt
verb:about
	msg:\y##NAME## \woriginal idea by \yRoger Firth\w.
		This C64 implementation has been created by \y##AUTHOR## with his \y##ENGINE##
obj:cloak
		name:a \yblack velvet cloak
	attr:takeable+visible
	desc:It is a handsome cloak, of velvet trimmed with satin, and slightly spattered with raindrops. Its blackness is so deep that it almost seems to suck light from the room.
	verb:x
		ifisroom:dark_bar
			msg:It's pitch black here. You can't see a thing.
	verb:take
		ifobjin:$this,$here
			putobj:$this,$inventory
			ifobj:$this,hung
				unsetobj:$this,hung			
			setroomoverlayimage:cloakroom,$null
			msg:Taken
	verb:drop|put_down
		ifisroom:dark_bar
			msg:It's pitch black here. You can't see a thing.
		else
			ifobjin:cloak,$inventory					
				ifisroom:cloakroom
					putobj:cloak,cloakroom
					setvar:$score,1
					setroomoverlayimage:cloakroom,png\cloakroom3_C64.png-cloakroom_C64.png
					msg:You drop you cloak on the floor
				else
					msg:This isn't the best place to leave a smart cloak lying around.
			else
				msg:You don't have the cloak with you anymore
	verb:hang|put_on
		ifisroom:dark_bar
			msg:It's pitch black here. You can't see a thing.
		else
			ifisroom:cloakroom
				ifobjin:cloak,$inventory					
					putobj:cloak,cloakroom
					setobj:cloak,hung
					setvar:$score,2
					setroomoverlayimage:cloakroom,png\cloakroom2_C64.png-cloakroom_C64.png
					msg:You hang the cloak on the hook.
				else
					ifobj:cloak,hung
						msg:The cloak is already hung up!
					else
						msg:You don't have the cloak with you anymore
			else
				msg:There is nowhere to hang the cloak around here!
room:foyer
	name:Foyer of the Opera House
	image:png\foyer_C64.png
	desc:You are standing in a spacious hall, splendidly decorated in red and gold, with glittering chandeliers overhead. The entrance from the street is to the \ynorth\w, and there are doorways \ysouth\w and \ywest\w.
	verb:s
		ifobjin:cloak,$inventory
			goto:dark_bar
		else
			goto:bar
	verb:w
		goto:cloakroom
	verb:n
		msg:You've only just arrived, and besides, the weather outside seems to be getting worse.
	scenery:hall
		desc:The hall is spacious and splendidly decorated in red and gold.
	scenery:entrance
		desc:The entrance is to the north.
	sceneryobj:chandeliers|chandelier		
				desc:They're overhead and glittering.
room:bar
	name:Foyer Bar
	image:png\foyerbar_C64.png
	desc:The bar, much rougher than you'd have guessed after the opulence of the foyer to the north, is completely empty. There seems to be some sort of message scrawled in the sawdust on the floor.		
	verb:n
		goto:foyer
	sceneryobj:message|floor|sawdust
		name:a \yscrawled message		
		verb:x
		verb:read
			ifvar:damage_number,<,2
				addvar:$score,1
				msg:The message, neatly marked in the sawdust, reads...
					\c
					\c\y*** YOU HAVE WON ***
					\c
			else
				msg:The message has been carelessly trampled, making it difficult to
					read. You can just distinguish the words...
					\c
					\c\y*** YOU HAVE LOST ***			
					\c
			msg:Your current score is +
			msg:$score
			waitkey
			msg:\c
				Yes, I know, this game is really short, but it's been fun for me to play
				with a classic while developing this little IF engine.
				\c
				Anyway...
				...do you want to play again? (Y/N)
			getkey
			ifkey:Y
				start
			else
				quit
room:cloakroom
	name:Cloakroom
	image:png\cloakroom_C64.png
	desc:The walls of this small room were clearly once lined with hooks, though now only one remains. The exit is a door to the \yeast\w.
	verb:e
		goto:foyer
	scenery:door
		desc:It's the door leading back to the foyer.
	scenery:wall|walls
		desc:The walls of this small room were clearly once lined with hooks, though now only one remains.
	scenery:hooks
		desc:There's only one of them.
	sceneryobj:hook|peg
		name:a \ysmall brass hook
		verb:x
			msg:It's just a small brass hook +
			ifobj:cloak,hung
				msg:with a cloak hanging on it.
			else
				msg:screwed to the wall.
room:dark_bar
	name:Darkness
	desc:It's pitch black here. You can't see a thing.
	verb:n
		goto:foyer
	verb:ne
	verb:e
	verb:se
	verb:w
	verb:sw
	verb:ne
	verb:nw
		msg:Blundering around in the dark isn't a good idea!
		addvar:damage_number,2
	verb:inventory
	verb:x
	verb:wait
	verb:read
	verb:switch_on
	verb:turn_on
	verb:drop
	verb:pick_up
	verb:take
	verb:throw
	verb:search
		msg:In the dark? You could easily disturb something!
		addvar:damage_number,1
room:$start
	image:png\cloakofdarkness_C64.png
	onfirst
		msg:Hurrying through the rainswept November night, you're glad to see	the bright lights of the Opera House. It's surprising that there aren't more people about but, hey, what do you expect in a cheap demo game...?			
		waitkey
		clear
		msg:\c\y##NAME##
			\c
			\c\wA native text+gfx C64 implementation
			\cby
			\c##AUTHOR##
			\c
			\c\gVersion ##VERSION##, January 2022
		waitkey
		clear
		setvar:$topscore,3
		putobj:cloak,$inventory
		goto:foyer
